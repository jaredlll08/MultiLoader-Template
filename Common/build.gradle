plugins {
    id 'java'
    id 'org.spongepowered.gradle.vanilla'
    id 'maven-publish'
}

archivesBaseName = "${mod_name}-common-${minecraft_version}"

minecraft {
    version(minecraft_version)

    runs {   
        if (Boolean.parseBoolean(common_runs_enabled as String)) {
            server(common_server_run_name as String) {
                workingDirectory(this.file("run"))
            }
            client(common_client_run_name as String) {
                workingDirectory(this.file("run"))
            }
        }
    }
}

dependencies {
    compileOnly 'org.spongepowered:mixin:0.8.5'
}

// Add the generated resources to the main source set
sourceSets.main.resources { srcDir 'src/generated/resources' }

processResources {
    def buildProps = new HashMap<>(project.properties)

    filesMatching(['pack.mcmeta']) {
        expand buildProps
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}